import qsl.internal.Versions

plugins {
	id("qsl.module")
}

qslModule {
	name = "Quilt DataFixerUpper API"
	moduleName = "datafixerupper"
	id = "quilt_datafixerupper"
	description = "Provides APIs for adding custom DataFixers."
	moduleDependencies {
		core {
			impl("lifecycle_events")
		}
	}
	entrypoints {
		events {
			values = ["org.quiltmc.qsl.datafixerupper.impl.ServerFreezer"]
		}
		client_events {
			values = ["org.quiltmc.qsl.datafixerupper.impl.client.ClientFreezer"]
		}
	}
}

sourceSets {
	testmodV1 {
		compileClasspath += main.compileClasspath
		runtimeClasspath += main.runtimeClasspath
	}
	testmodV2 {
		compileClasspath += main.compileClasspath
		runtimeClasspath += main.runtimeClasspath
	}
	testmodV3 {
		compileClasspath += main.compileClasspath
		runtimeClasspath += main.runtimeClasspath
	}
	testmodNoop {
		compileClasspath += main.compileClasspath
		runtimeClasspath += main.runtimeClasspath
	}
}

repositories {
	maven {
		name = "Gegy's Maven"
		url = "https://maven.gegy.dev/"
		content {
			includeGroup "supercoder79"
		}
	}

	maven {
		name = "Modrinth Maven"
		url = "https://api.modrinth.com/maven"
		content {
			includeGroupByRegex "maven\\.modrinth"
		}
	}
}

loom {
	runs {
		autoTestServerDfuTestmodV1 {
			server()
			configName = "Auto test server - DFU testmod v1"
			source(project.sourceSets.testmodV1)
			property("quilt.auto_test")
			programArg("--nogui")
		}

		autoTestServerDfuTestmodV2 {
			server()
			configName = "Auto test server - DFU testmod v2"
			source(project.sourceSets.testmodV2)
			property("quilt.auto_test")
			programArg("--nogui")
		}

		autoTestServerDfuTestmodV3 {
			server()
			configName = "Auto test server - DFU testmod v3"
			source(project.sourceSets.testmodV3)
			property("quilt.auto_test")
			programArg("--nogui")
		}

		Versions.DATABREAKER_VERSION.ifPresent {
			autoTestServerDfuTestmodNoop {
				server()
				configName = "Auto test server - DFU testmod no-op"
				source(project.sourceSets.testmodNoop)
				property("quilt.auto_test")
				programArg("--nogui")
				runDir("run/noop")
			}
		}
	}

	createRemapConfigurations(sourceSets.testmodV1)
	createRemapConfigurations(sourceSets.testmodV2)
	createRemapConfigurations(sourceSets.testmodV3)
	createRemapConfigurations(sourceSets.testmodNoop)
}

dependencies {
	testmodV1Implementation sourceSets.main.output
	testmodV2Implementation sourceSets.main.output
	testmodV3Implementation sourceSets.main.output

	// TODO: wait for loom to actually work and not infect unrelated source sets
	Versions.DATABREAKER_VERSION.ifPresent {
		modTestmodNoopRuntimeOnly("supercoder79:databreaker:${it}") {
			transitive = false
		}
	}
	Versions.LAZYDFU_VERSION.ifPresent {
		modTestmodV1RuntimeOnly("maven.modrinth:lazydfu:${it}") {
			transitive = false
		}
	}
}

task prepareDfuTestmod {
	doLast {
		delete "run"
		mkdir "run"
		mkdir "run/noop"
		logger.quiet("NOTE: By running this testmod, you automatically agree to the EULA.")
		file("run/eula.txt").text = "eula=true"
		file("run/noop/eula.txt").text = "eula=true"

		// We want fast world generation for those tests.
		// Change the port to another one so it doesn't conflict with the main test server in case of running in parallel.
		String properties = '''\
level-type=flat
allow-nether=false
server-port=25585
'''
		file("run/server.properties").text = properties
		file("run/noop/server.properties").text = properties
	}
}

afterEvaluate {
	// Apparently we need an after evaluate to not mess up source set dependencies.

	runAutoTestServerDfuTestmodV1.dependsOn prepareDfuTestmod
	runAutoTestServerDfuTestmodV2.dependsOn runAutoTestServerDfuTestmodV1
	runAutoTestServerDfuTestmodV3.dependsOn runAutoTestServerDfuTestmodV2

	rootProject.tasks.runAutoAllTestServer.dependsOn runAutoTestServerDfuTestmodV3

	Versions.DATABREAKER_VERSION.ifPresent {
		runAutoTestServerDfuTestmodNoop.dependsOn prepareDfuTestmod
		rootProject.tasks.runAutoAllTestServer.dependsOn runAutoTestServerDfuTestmodNoop
	}
}
