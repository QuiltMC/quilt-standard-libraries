import java.nio.file.Files

pluginManagement {
	repositories {
		gradlePluginPortal()
		mavenCentral()

		maven {
			name "Quilt"
			url "https://maven.quiltmc.org/repository/snapshot"
		}
		maven {
			name "Quilt"
			url "https://maven.quiltmc.org/repository/release"
		}
		maven {
			name "Fabric"
			url "https://maven.fabricmc.net"
		}
	}
}

rootProject.name = "qsl"

// Libraries
library("core")
library("block")
library("data")
library("entity")
library("rendering")
library("gui")
library("item")
library("management")
library("worldgen")
library("misc")

def library(String library) {
	include(library)

	def libraryProject = project(":$library")
	libraryProject.projectDir = file("library/$library")

	if (Files.exists(libraryProject.projectDir.toPath().resolve("DISABLE"))) {
		return
	}

	rootProject.projectDir.toPath().resolve("library/$library/").toFile().listFiles().each {
		// Is the module disabled?
		if (it.isDirectory()
				&& it.name != "src" // Ignore sources
				&& it.name != "build" // Ignore build artifacts
				&& !it.name.startsWith(".") // Ignore anything hidden on unix-like OSes
		) {
			// Libraries can be disabled by adding a file named DISABLE at the root of its directory
			if (Files.exists(it.toPath().resolve("build.gradle")) && Files.notExists(it.toPath().resolve("DISABLE"))) {
				include "$library:$it.name"
			}
		}
	}
}
