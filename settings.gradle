import java.nio.file.Files

pluginManagement {
	repositories {
		gradlePluginPortal()
		mavenCentral()

		maven {
			name = "Quilt"
			url = "https://maven.quiltmc.org/repository/release"
		}
	}
}

rootProject.name = "qsl"

// Build logic is an included build for holding the convention plugins
includeBuild("build-logic")

library("core")

def library(String library) {
	include(library)
	project(":$library").projectDir = file("library/$library")

	rootProject.projectDir.toPath().resolve("library/$library/").toFile().listFiles().each {
		if (it.isDirectory() && it.name != "src" && it.name != "build" && !it.name.startsWith(".")) {
			def properties = new Properties()
			properties.load(Files.newBufferedReader(it.toPath().resolve("gradle.properties")))

			if (!properties.qsl_disabled) {
				include "$library:$it.name"
			}
		}
	}
}
