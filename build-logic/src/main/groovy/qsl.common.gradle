/*
 * Common buildscript for QSL in projects which depend on Minecraft, whether for the API or for running the game.
 */

import qsl.internal.Versions;

plugins {
	id("maven-publish")
	id("fabric-loom")
	id("org.quiltmc.quilt-mappings-on-loom")
	id("io.github.juuxel.loom-quiltflower")
}

def ENV = System.getenv()

publishing {
	repositories {
		if (ENV.MAVEN_URL) {
			maven {
				url ENV.MAVEN_URL

				credentials {
					username ENV.MAVEN_USERNAME
					password ENV.MAVEN_PASSWORD
				}
			}
		} else if (ENV.SNAPSHOTS_URL) {
			maven {
				url ENV.SNAPSHOTS_URL

				credentials {
					username ENV.SNAPSHOTS_USERNAME
					password ENV.SNAPSHOTS_PASSWORD
				}
			}
		} else {
			mavenLocal()
		}
	}
}
repositories {
	maven {
		name "Quilt Snapshots"
		url "https://maven.quiltmc.org/repository/snapshot"
	}
}
dependencies {
	minecraft "com.mojang:minecraft:${Versions.MINECRAFT_VERSION}"
	mappings(loom.layered {
		addLayer(quiltMappings.mappings("org.quiltmc:quilt-mappings:${Versions.MINECRAFT_VERSION}+build.${Versions.MAPPINGS_BUILD}:v2"))
	})

	modImplementation "org.quiltmc:quilt-loader:${Versions.LOADER_VERSION}"
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release.set(Versions.JAVA_VERSION)
}

loom {
	// Upstream Loom breaks this so we can't have nice things until the quilt-gradle replacement
	//shareRemapCaches = true
}
