/*
 * Buildscript for defining a QSL library.
 *
 * This is a convention plugin, and is applied by adding the following to your `plugins` block: `id("qsl.library")`.
 * A library is not exactly a mod, but rather a maven artifact that depends on a set of modules. For example, the `core`
 * library may depend on a module to provide the event code, code to add Quilt mods to the crash report, etc.
 *
 * This buildscript declares an extension available under the `qslLibrary` name. This extension is used to configure
 * attributes of the library, namely the name of the library and the library's version. The buildscript also declares
 * some additional tasks that can be used to run the game and runTestmodClient, runTestmodServer. These testmod tasks
 * can be run using `gradle LIBRARY_NAME:TASK_NAME`.
 */

import qsl.internal.Git
import qsl.internal.extension.QSLLibraryExtension

plugins {
	id("maven-publish")
	id("net.kyori.indra.git")
	id("qsl.common")
}

def extension = project.extensions.create("qslLibrary", QSLLibraryExtension, project)

// We seem to have to use afterEvaluate to verify this stuff
afterEvaluate {
	// TODO: Validate the extension parts
}

sourceSets {
	testmod {
		compileClasspath += main.compileClasspath
		runtimeClasspath += main.runtimeClasspath
	}
}

group = "org.quiltmc.qsl"

// Because we rely on the results of our submodules, we use afterEvaluate to setup the sourcesets and testmod tasks.
afterEvaluate {
	// Make this project include the compileClasspath and runtimeClasspath of the sub-modules.
	subprojects.each {
		project.sourceSets.main.compileClasspath += it.sourceSets.main.compileClasspath
		project.sourceSets.main.runtimeClasspath += it.sourceSets.main.runtimeClasspath
		project.sourceSets.testmod.compileClasspath += it.sourceSets.testmod.compileClasspath
		project.sourceSets.testmod.runtimeClasspath += it.sourceSets.testmod.runtimeClasspath
	}

	loom {
		// Define testmod tasks.
		// These tasks allow you to run with testmods on the runtime classpath and will generate IDE run tasks.
		runs {
			testmodClient {
				client()
				name = "Testmod Client"
				source(project.sourceSets.testmod)
			}

			testmodServer {
				server()
				name = "Testmod Server"
				source(project.sourceSets.testmod)
			}

			// Auto test server, a server is ran for a few seconds and testmods run to verify things such as mixin
			// application function properly.
			// This task is typically ran by the CI server.
			autoTestServer {
				server()
				name = "Auto test server"
				source(project.sourceSets.testmod)
				property("quilt.autoTest")
				programArg("--nogui")
			}
		}
	}
}

// A library isn't truly a distributed artifact, rather it is just maven metadata to depend on the modules of a library.
// For that reason, we have no use for the jar or remapJar tasks.
jar {
	enabled = false
}

remapJar {
	enabled = false
}

task checkLibVersion {
	doFirst {
		def libraryName = extension.libraryName

		try {
			def xml = new URL("https://maven.quiltmc.org/repository/release/org/quiltmc/qsl/$libraryName/$project.version/" +
					"$libraryName-$project.version" + ".pom").text
			def metadata = new groovy.xml.XmlSlurper().parseText(xml)

			if (metadata.properties.hash != Git.getLatestCommitHash(project)) {
				throw new RuntimeException("Library is already published with a different hash!")
			}
		} catch (FileNotFoundException ignored) {
			//
		}
	}
}

project.getTasksByName("checkModuleVersion", true).each {
	checkLibVersion.mustRunAfter it
}

publishing {
	repositories {
		mavenLocal()
	}

	publications {
		mavenJava(MavenPublication) {
			pom.withXml {
				asNode().appendNode("properties").appendNode("hash", Git.getLatestCommitHash(project))
				def depsNode = asNode().appendNode("dependencies")
				rootProject.subprojects.stream().filter {
					it.path.startsWith(":$extension.libraryName:")
				}.forEach {
					def depNode = depsNode.appendNode("dependency")
					depNode.appendNode("groupId", it.group)
					depNode.appendNode("artifactId", it.name)
					depNode.appendNode("version", it.version)
					depNode.appendNode("scope", "compile")
				}
			}
		}
	}
}

publish.dependsOn checkLibVersion
